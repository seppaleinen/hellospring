group = 'se.david'
version = '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.0.RELEASE'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('io.springfox:springfox-swagger2:2.9.2')    // Swagger dependency
    compile('io.springfox:springfox-swagger-ui:2.9.2')  // Swagger dependency

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.rest-assured:rest-assured:3.0.5')
    testCompile('junit:junit:4.12')
    testCompile('org.springframework.restdocs:spring-restdocs-restassured:2.0.0.RELEASE')
    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.0.RELEASE"
}

// Spring Rest Docs
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    testLogging {
        showStandardStreams = false
    }
    outputs.dir snippetsDir // Spring Rest Docs
}

// Spring Rest Docs
asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
    outputDir 'build/asciidoc'
    attributes[
            'toc=left',
            'source-highlighter=highlightjs',
            'toc-title=Table of Contents'
    ]
    attributes 'snippets': snippetsDir

    sourceDir 'src/docs/asciidoc'
}

// Spring Rest Docs
task copyRestDocs(type: Copy) {
    dependsOn asciidoctor
    from "${asciidoctor.outputDir}/html5"
    into "${sourceSets.main.output.resourcesDir}/public/docs"
}

// Spring Rest Docs
bootJar {
    dependsOn copyRestDocs
}
